apply plugin: 'application'

sourceCompatibility = 1.8

mainClassName = "depsolver.Depsolver"

repositories {
    ivy {
        artifactPattern "https://www.sosy-lab.org/ivy/[organisation]/[module]/[artifact]-[revision](-[classifier]).[ext]"
        ivyPattern "https://www.sosy-lab.org/ivy/[organisation]/[module]/ivy-[revision].xml"
    }
    mavenCentral()
}

dependencies {

    implementation ( group: "org.sosy_lab", name: "javasmt", version: "1.0.1-219-g535f988", configuration: "runtime-z3") {
        exclude group: "org.sosy_lab", module: "javasmt-solver-z3"
    }

    implementation ("com.alibaba:fastjson:1.2.46")

    implementation fileTree(dir: "src/main/resources/", include: "*.jar")

}

jar {
    manifest {
        attributes "Implementation-Title": "Dependency Solver",
                "Main-Class": "$mainClassName",
                "Class-Path": configurations.runtimeClasspath.collect {
                    f ->
                        if (f.getPath().endsWith(".jar")) {
                            f.getPath()
                        } else {
                            ""
                        }
                }.join(" ")
    }
}

// Allow java to find Z3 binaries and parse command-line args
tasks.withType(JavaExec) {
    if (project.hasProperty("cmdArgs")) {
        args Eval.me(cmdArgs)
    }
}
